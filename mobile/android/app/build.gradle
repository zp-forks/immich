plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "kotlin-kapt"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { localProperties.load(it) }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withInputStream { keystoreProperties.load(it) }
}

android {
    compileSdkVersion 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "app.alextran.immich"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

   signingConfigs {
       release {
            def keyAliasVal = System.getenv("ALIAS")
            def keyPasswordVal = System.getenv("ANDROID_KEY_PASSWORD")
            def storePasswordVal = System.getenv("ANDROID_STORE_PASSWORD")

            keyAlias keyAliasVal ? keyAliasVal : keystoreProperties['keyAlias']
            keyPassword keyPasswordVal ? keyPasswordVal : keystoreProperties['keyPassword']
            storeFile file("../key.jks") ? file("../key.jks") : file(keystoreProperties['storeFile'])
            storePassword storePasswordVal ? storePasswordVal : keystoreProperties['storePassword']
       }
   }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }

        release {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    def kotlin_version = '1.9.24'
    def kotlin_coroutines_version = '1.8.1'
    def work_version = '2.9.0'
    def concurrent_version = '1.1.0'
    def guava_version = '33.2.0-android'
    def glide_version = '4.16.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.concurrent:concurrent-futures:$concurrent_version"
    implementation "com.google.guava:guava:$guava_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
}

// This is uncommented in F-Droid build script
//f configurations.all {
//f     exclude group: 'com.google.android.gms'
//f }
